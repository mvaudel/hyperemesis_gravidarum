import os
import pandas as pd

##
#
# This script extracts summary stats for variants of interest.
#
# Commands to execute from the repository folder:
# conda activate snakemake
# snakemake --snakefile src/look_up/extract_snps.snake --cores 32 --use-conda --rerun-incomplete
##

config_file = 'src/look_up/look_up.yaml'
configfile: config_file

variants_file = 'src/look_up/resources/look_up_snps_24.06.05'
variants_data = pd.read_csv(
    variants_file,
    sep = '\t',
    header = 0,
    na_values = ['NA']
)
variants = variants_data["id"]
variants_string = "|".join(variants)

analyses = config["analyses"]
analyses_names = list(analyses.keys())

gwas_version = config["gwas_version"]
look_up_version = config["look_up_version"]

regenie_folder = work_folder = os.path.join('/mnt/work/marc/moba/hyperemesis_gravidarum', gwas_version, 'results')
results_folder = "src/look_up/resources"


####################### Housekeeping #########################################

if not os.path.isdir(results_folder):
    os.makedirs(results_folder)


####################### RUN SETTINGS ###########################################




############################## Expected output #################################

def get_output():
    output_files = list()
    for analysis in analyses_names:
        phenotype = analyses[analysis]["phenotype"]
        for population in analyses[analysis]["populations"]:
            output_files.append(
                os.path.join(results_folder, analysis, f"pop_{population}_pheno_{phenotype}.txt")
            )
    return output_files



############################## RULES ###########################################

print(f"Extracting {variants_string} in {analyses_names}.")
rule all:
    'The final output rule'
    input:
        output_file = os.path.join(results_folder, f"look_up_merged_{look_up_version}.gz")


rule extract_snps:
    'Extracts the SNPs of interest from the results of regenie.'
    input:
        regenie_file = os.path.join(regenie_folder, '{analysis}', 'step2_pop_{population}_pheno_{phenotype}.regenie.gz')
    params:
        variants_string = variants_string
    output:
        look_up_file = os.path.join(results_folder, '{analysis}', "pop_{population}_pheno_{phenotype}.txt")
    threads: 1
    shell:
        """
        zcat {input.regenie_file} | awk 'NR==1 || $0 ~ /{params.variants_string}/' > {output.look_up_file}
        """

rule merge_wlm:
    'Merges the individual files and runs WLM'
    input:
        md_files = get_output()
    params:
        look_up_folder = results_folder,
        config_file = config_file
    output:
        output_file = os.path.join(results_folder, f"look_up_merged_{look_up_version}.gz")
    conda:
        "envs/r_merge_wlm.yaml"
    threads: 1
    shell:
        """
        Rscript src/look_up/utils/merge_and_wlm.R {params.config_file} {params.look_up_folder} {output.output_file}
        """

